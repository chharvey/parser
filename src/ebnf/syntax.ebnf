// IDENTIFIER  ::= [./lexicon.ebnf#Identifier];
// CHAR_CODE   ::= [./lexicon.ebnf#CharCode];
// STRING      ::= [./lexicon.ebnf#String];
// CHAR_CLASS  ::= [./lexicon.ebnf#CharClass];



ParameterSet ::= "<"                                IDENTIFIER             # ">";
ArgumentSet  ::= "<" (("+" | "-" | "âˆ“" | "?" | "!") IDENTIFIER            )# ">";
ConditionSet ::= "<" (                              IDENTIFIER ("+" | "-"))# ">";



Reference ::=
	| IDENTIFIER
	| Reference ArgumentSet;



Unit ::=
	| CHAR_CODE
	| STRING
	| CHAR_CLASS
	| Reference
	| "(" Definition ")"
;



Unary
	::= Unit ("+" | "*" | "#")? "?"?;



Item ::=
	| Unary
	| ConditionSet Item
;


Seq      ::= (Seq      "&"?)? Item;
UnSeq    ::= (UnSeq    "&&")? Seq;
UnChoice ::= (UnChoice "||")? Unseq;
Choice   ::= (Choice   "|" )? UnChoice;



Definition
	::= ("&" | "&&" | "||" | "|")? Choice;



NonterminalName ::=
	| IDENTIFIER
	| NonterminalName ParameterSet;



Production
	::= NonterminalName (":::=" | "::=") Definition ";";



Goal
	::= #x02 Production* #x03;
